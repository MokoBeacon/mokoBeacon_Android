apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "com.moko.beacon"
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 7
        versionName "1.3.0"

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = true
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    signingConfigs {
        debug {
            v1SigningEnabled true
            v2SigningEnabled true
        }
        release {
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def formatedDate = new Date().format("yyyyMMddHHmm")
                def apkFileName = "MokoBeacon_${defaultConfig.versionName}_${formatedDate}.apk"
                if (outputFile.name.contains('debug')) {
                    apkFileName = "MokoBeacon_${defaultConfig.versionName}_${formatedDate}_debug.apk"
                }
                outputFileName = apkFileName
            }
        }
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '28.0.0'
            }
        }
    }
}

dependencies {
    implementation fileTree(include: '*.jar', dir: 'libs')
    implementation 'com.jakewharton:butterknife:7.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation project(path: ':mokosupport')

}
